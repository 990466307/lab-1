/*
 * Tai-e: A Static Analysis Framework for Java
 *
 * Copyright (C) 2020-- Tian Tan <tiantan@nju.edu.cn>
 * Copyright (C) 2020-- Yue Li <yueli@nju.edu.cn>
 * All rights reserved.
 *
 * Tai-e is only for educational and academic purposes,
 * and any form of commercial use is disallowed.
 * Distribution of Tai-e is disallowed without the approval.
 */

plugins {
    id 'java'
    id 'application'
}

repositories {
    mavenCentral()
    maven { // repository for Soot snapshot
        url 'https://oss.sonatype.org/content/repositories/snapshots'
    }
}

dependencies {
    implementation(
            // Process options
            'info.picocli:picocli:4.6.1',
            // logger
            'org.apache.logging.log4j:log4j-api:2.16.0',
            'org.apache.logging.log4j:log4j-core:2.16.0',
            // Process YAML configuration files
            'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.12.2',
            // Use Soot as frontend
            files('lib/sootclasses-modified.jar'),
            'org.soot-oss:soot:4.3.0-20211223.212205-256',
            // Eliminate SLF4J warning
            'org.slf4j:slf4j-simple:1.7.5',
    )
    testImplementation(
            'junit:junit:4.13',
    )
}

application {
    mainClass.set('pascal.taie.Main')
}

task allInOne(type: Jar) {
    description 'Creates a single jar file including Tai-e and all dependencies'
    manifest {
        attributes 'Main-Class': 'pascal.taie.Main'
    }
    archiveBaseName = 'tai-e-all'
    from {
        configurations.compileClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    with jar
}

task assignments(dependsOn: allInOne, type: JavaExec) {
    description 'Generates assignment packages'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'pascal.taie.util.AssignmentMaker'
    def cmdArgs = project.hasProperty("args") ?
            project.getProperty("args") : ""
    args cmdArgs.split()
}
